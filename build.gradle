import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    // https://github.com/Goooler/shadow
    id 'io.github.goooler.shadow' version '8.1.7'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group 'me.datatags.ultracosmetics'
version '1.1.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    // SpigotMC
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    // Dependencies
    maven {
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }
    // UltraCosmetics
    maven {
        url = 'https://jitpack.io/'
    }
}

dependencies {
    library 'com.google.code.gson:gson:2.11.0'
    compileOnly 'org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT'
    compileOnly 'com.github.UltraCosmetics:UltraCosmetics:3.5-DEV-b1'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

shadowJar {
    minimize()
    def path = 'me.datatags.ultracosmeticsguiaddon.shaded'
    relocate('org.bstats', path + '.bstats')
}


bukkit {
    main = 'me.datatags.ultracosmeticsguiaddon.UltraCosmeticsGUIAddon'
    apiVersion = '1.17'
    authors = ['datatags', 'dennishzg']
    depend = ['UltraCosmetics']
}

processResources {
    inputs.property("version", project.version)

    filter ReplaceTokens, tokens: [
            'VERSION'   : project.version,
    ]
}