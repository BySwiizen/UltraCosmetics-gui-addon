import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    // https://github.com/Goooler/shadow
    id 'io.github.goooler.shadow' version '8.1.8'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group 'me.datatags.ultracosmetics'
version '1.2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    // SpigotMC
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // Dependencies
    maven { url = 'https://repo.codemc.io/repository/maven-snapshots/' }
    // UltraCosmetics
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    // https://github.com/google/gson
    library 'com.google.code.gson:gson:2.11.0'
    // https://github.com/KyoriPowered/adventure
    library 'net.kyori:adventure-platform-bukkit:4.3.3'
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT'
    // https://github.com/UltraCosmetics/UltraCosmetics
    compileOnly 'com.github.UltraCosmetics:UltraCosmetics:3.6.0.1-RELEASE'
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

shadowJar {
    minimize()
    def path = 'me.datatags.ultracosmeticsguiaddon.shaded'
    relocate('org.bstats', path + '.bstats')
}

bukkit {
    name = 'UltraCosmeticsGUIAddon'
    authors = ['datatags', 'dennishzg']
    main = 'me.datatags.ultracosmeticsguiaddon.UltraCosmeticsGUIAddon'
    apiVersion = '1.17'
    depend = ['UltraCosmetics']
}

processResources {
    inputs.property("version", project.version)

    filter ReplaceTokens, tokens: [
            'VERSION'   : project.version,
    ]
}
