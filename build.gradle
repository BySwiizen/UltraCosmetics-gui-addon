import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    // https://github.com/Goooler/shadow
    id 'io.github.goooler.shadow' version '8.1.7'
}

group 'me.datatags.ultracosmetics'
version '1.1.1'

repositories {
    mavenLocal()
    mavenCentral()

    // SpigotMC
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    // Dependencies
    maven {
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }
    // UltraCosmetics
    maven {
        url = 'https://jitpack.io/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.3-R0.1-SNAPSHOT'
    compileOnly 'com.github.UltraCosmetics:UltraCosmetics:3.5-DEV-b1'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

shadowJar {
    minimize()
    def path = 'me.datatags.ultracosmeticsguiaddon.shaded'
    relocate('org.bstats', path + '.bstats')
}

processResources {
    inputs.property("version", project.version)

    filter ReplaceTokens, tokens: [
            'VERSION'   : project.version,
    ]
}